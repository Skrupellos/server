# Maintainer: Skupellos
pkgname=facette
pkgver=0.1.2
pkgrel=1
pkgdesc="Time series data visualization and graphing software"
arch=('i686' 'x86_64')
url="https://facette.io/"
license=('BSD')
depends=('rrdtool')
makedepends=('nodejs' 'go')
backup=('etc/facette/facette.json' 'etc/facette/providers/collectd.json')
install=facette.install
source=(
  "https://github.com/facette/facette/archive/$pkgver.tar.gz"
  "https://github.com/facette/facette/releases/download/$pkgver/facette-$pkgver-linux-amd64.tar.gz"
  'facette.install'
  'facette.service'
  'unix-socket.patch'
)
md5sums=('e097dca1c96b2c4dbbe3d80259f50c12'
         '0833d0bcaaf2a1b080b1c97485e72e81'
         '1426059566937df352b92d3a40754d97'
         '3cd4bfdfdfe2d8fc655c13bae64af025'
         '6b98d8cd87b232020478e08e114a05bd')


prepare() {
  cd "$pkgname-$pkgver"
  
  patch -p1 -i "$srcdir/unix-socket.patch"
}

build() {
  cd "$pkgname-$pkgver"
  
  make build-bin
  make build-static
}

package() {
  install -Dm0644 'facette.service' "$pkgdir/usr/lib/systemd/system/facette.service"
  install -d "$pkgdir/var/lib/facette/"
  
  
  cd "$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr/" install-bin
  make PREFIX="$pkgdir/usr/" install-static
  
  install -Dm0644 'docs/examples/facette.json'            "$pkgdir/etc/facette/facette.json"
  install -Dm0644 'docs/examples/providers/collectd.json' "$pkgdir/etc/facette/providers/collectd.json"
  sed -i 's#/var/run/#/run/#' "$pkgdir/etc/facette/facette.json"
  
  install -Dm0644 'LICENSE' "$pkgdir/usr/share/licenses/facette/LICENSE"
  
  
  cd ../facette-linux-amd64
  
  install -Dm0644 'man/facette.1'    "$pkgdir/usr/share/man/man1/facette.1"
  install -Dm0644 'man/facettectl.8' "$pkgdir/usr/share/man/man8/facettectl.8"
}

check() {
  cd "$pkgname-$pkgver"
  
  ## Currently failing
  #make test
}
